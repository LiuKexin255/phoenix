load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template")
load("@rules_go//go:def.bzl", "go_binary", "go_library")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_push")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

# gazelle:resolve go phoenix/project/infra/object_storage //project/infra/object_storage:api_gateway_library

go_library(
    name = "gateway_lib",
    srcs = ["main.go"],
    importpath = "phoenix/project/infra/object_storage/gateway",
    visibility = ["//visibility:private"],
    deps = [
        "//common/go/otel",
        "//project/infra/object_storage:api_gateway_library",
        "@com_github_grpc_ecosystem_grpc_gateway_v2//runtime",
        "@io_opentelemetry_go_contrib_instrumentation_google_golang_org_grpc_otelgrpc//:otelgrpc",
        "@io_opentelemetry_go_contrib_instrumentation_net_http_otelhttp//:otelhttp",
        "@io_opentelemetry_go_otel//propagation",
        "@org_golang_google_grpc//:grpc",
        "@org_golang_google_grpc//credentials/insecure",
        "@org_golang_google_grpc//encoding/gzip",
        "@org_golang_google_grpc//keepalive",
    ],
)

go_binary(
    name = "gateway",
    embed = [":gateway_lib"],
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "gateway_pkg",
    srcs = [":gateway"],
)

oci_image(
    name = "image",
    base = "@ubuntu",
    entrypoint = ["/gateway"],
    env = {
        "GRPC_GO_LOG_SEVERITY_LEVEL": "info",
    },
    exposed_ports = ["443"],
    tars = [":gateway_pkg"],
)

expand_template(
    name = "stamped",
    out = "_stamped.tags.txt",
    stamp_substitutions = {"v0.0.1": "v0.0.1-{{BUILD_EMBED_LABEL}}"},
    template = ["v0.0.1"],
)

oci_push(
    name = "push",
    image = ":image",
    remote_tags = ":stamped",
    repository = "docker.liukexin.com/artificial/infra/object_storage.api",
)
